plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.22' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.22' apply false
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.22' apply false
    id 'org.springframework.boot' version '3.2.2' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id "com.vanniktech.maven.publish" version "0.30.0" apply false
}

allprojects {
    group = 'io.github.danpung2'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    // Kotlin Compile Option
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += '-Xjsr305=strict'
            jvmTarget = '17'
        }
    }

    // JUnit 5
    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }


    apply plugin: 'jacoco'
    jacoco {
        toolVersion = "0.8.11"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
        dependsOn test
    }

    if (project.name != 'streamsheet-demo') {
    }

    plugins.withId("signing") {
        signing {
            def signingKey = findProperty("signing.secretKey")
            def signingPassword = findProperty("signing.password")
            if (signingKey && signingPassword) {
                useInMemoryPgpKeys(signingKey, signingPassword)
            }
        }
    }
}
